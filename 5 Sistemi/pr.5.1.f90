! ============================================================
!  Програм: gauss_elimination.f90
!  Цел: Решава систем од линеарни равенки A*x = b
!       со методот на Гаусова елиминација.
! ============================================================

program gauss_elimination
    implicit none
    integer, parameter :: n = 3      ! број на непознати
    real(8) :: A(n,n), b(n), x(n)
    real(8) :: factor
    integer :: i, j, k

    ! ------------------------------------------------------------
    ! ВЛЕЗНИ ПОДАТОЦИ
    ! ------------------------------------------------------------
    ! Пример систем:
    !   2x +  y -  z =  8
    !  -3x -  y + 2z = -11
    !  -2x +  y + 2z = -3
    !
    ! Матрицата A ги содржи коефициентите пред непознатите
    ! Векторот b ги содржи слободните членови (десната страна)
    ! ------------------------------------------------------------

    A = reshape([ 2.0d0,  1.0d0, -1.0d0, &
                 -3.0d0, -1.0d0,  2.0d0, &
                 -2.0d0,  1.0d0,  2.0d0 ], shape(A), order=[2,1])

    b = [ 8.0d0, -11.0d0, -3.0d0 ]

    print *, "------------------------------------------"
    print *, "Pochetna matrica [A|b]:"
    do i = 1, n
        print '(3f10.3,2x,f10.3)', A(i,1:n), b(i)
    end do

    ! ============================================================
    ! ФАЗА 1: ЕЛИМИНАЦИЈА НАНАПРЕД
    ! ============================================================
    ! Цел: Да се добие горна тријаголна матрица (нули под дијагоналата)
    ! Идеја: Со редни операции ги отстрануваме променливите под секој pivot
    ! ============================================================

    do k = 1, n-1
        ! k е индексот на редицата во која се наоѓа тековниот pivot елемент
        if (A(k,k) == 0.0d0) then
            print *, "Greshka: nula pivot, potrebno e pivotiranje!"
            stop
        end if

        ! Елиминирај ги елементите под pivot во колоната k
        do i = k+1, n
            factor = A(i,k) / A(k,k)
            ! Потсетување: R_i ← R_i - factor * R_k
            do j = k, n
                A(i,j) = A(i,j) - factor * A(k,j)
            end do
            b(i) = b(i) - factor * b(k)
        end do

        ! Печатење по секоја елиминација за следење на процесот
        print *
        print *, "Po eliminacija na kolonata", k
        do i = 1, n
            print '(3f10.3,2x,f10.3)', A(i,1:n), b(i)
        end do
    end do

    ! ============================================================
    ! ФАЗА 2: ЗАМЕНА НАНАЗАД
    ! ============================================================
    ! Цел: Откако добивме горна триаголна матрица, решаваме:
    !      U * x = b
    !      почнувајќи од последната равенка нагоре.
    ! ============================================================

    x(n) = b(n) / A(n,n)
    do i = n-1, 1, -1
        x(i) = b(i)
        do j = i+1, n
            x(i) = x(i) - A(i,j)*x(j)
        end do
        x(i) = x(i) / A(i,i)
    end do

    ! ============================================================
    ! ПЕЧАТЕЊЕ НА РЕЗУЛТАТОТ
    ! ============================================================
    print *
    print *, "------------------------------------------"
    print *, "Reshenieto na sistemot e:"
    do i = 1, n
        print *, "x(", i, ") = ", x(i)
    end do
    print *, "------------------------------------------"

end program gauss_elimination
